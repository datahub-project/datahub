buildscript {
  ext.junitJupiterVersion = '5.6.1'
  ext.gmaVersion = '0.2.45'
  ext.pegasusVersion = '28.3.7'

  apply from: './repositories.gradle'
  buildscript.repositories.addAll(project.repositories)
  dependencies {
    classpath 'com.linkedin.pegasus:gradle-plugins:' + pegasusVersion
    classpath 'com.github.node-gradle:gradle-node-plugin:2.2.4'
    classpath 'com.commercehub.gradle.plugin:gradle-avro-plugin:0.8.1'
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.2.RELEASE'
  }
}

project.ext.spec = [
    'product' : [
        'pegasus' : [
            'd2' : 'com.linkedin.pegasus:d2:' + pegasusVersion,
            'data' : 'com.linkedin.pegasus:data:' + pegasusVersion,
            'dataAvro1_6' : 'com.linkedin.pegasus:data-avro-1_6:' + pegasusVersion,
            'generator': 'com.linkedin.pegasus:generator:' + pegasusVersion,
            'restliCommon' : 'com.linkedin.pegasus:restli-common:' + pegasusVersion,
            'restliClient' : 'com.linkedin.pegasus:restli-client:' + pegasusVersion,
            'restliDocgen' : 'com.linkedin.pegasus:restli-docgen:' + pegasusVersion,
            'restliServer' : 'com.linkedin.pegasus:restli-server:' + pegasusVersion,
            'restliSpringBridge': 'com.linkedin.pegasus:restli-spring-bridge:' + pegasusVersion,
        ]
    ]
]

project.ext.externalDependency = [
    'antlr4Runtime': 'org.antlr:antlr4-runtime:4.7.2',
    'antlr4': 'org.antlr:antlr4:4.7.2',
    'assertJ': 'org.assertj:assertj-core:3.11.1',
    'avro_1_7': 'org.apache.avro:avro:1.7.7',
    'avroCompiler_1_7': 'org.apache.avro:avro-compiler:1.7.7',
    'cacheApi' : 'javax.cache:cache-api:1.1.0',
    'commonsIo': 'commons-io:commons-io:2.4',
    'commonsLang': 'commons-lang:commons-lang:2.6',
    'ebean': 'io.ebean:ebean:11.33.3',
    'ebeanAgent': 'io.ebean:ebean-agent:11.27.1',
    'elasticSearchRest': 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.9.3',
    'elasticSearchTransport': 'org.elasticsearch.client:transport:7.9.3',
    'findbugsAnnotations': 'com.google.code.findbugs:annotations:3.0.1',
    'gmaCoreModels': "com.linkedin.datahub-gma:core-models-data-template:$gmaVersion",
    'gmaDaoApi': "com.linkedin.datahub-gma:dao-api:$gmaVersion",
    'gmaDaoApiDataTemplate': "com.linkedin.datahub-gma:dao-api-data-template:$gmaVersion",
    'gmaEbeanDao': "com.linkedin.datahub-gma:ebean-dao:$gmaVersion",
    'gmaElasticsearchDao': "com.linkedin.datahub-gma:elasticsearch-dao-7:$gmaVersion",
    'gmaElasticsearchIntegTest': "com.linkedin.datahub-gma:elasticsearch-dao-integ-testing-docker-7:$gmaVersion",
    'gmaNeo4jDao': "com.linkedin.datahub-gma:neo4j-dao:$gmaVersion",
    'gmaRestliResources': "com.linkedin.datahub-gma:restli-resources:$gmaVersion",
    'gmaRestliResourcesDataTemplate': "com.linkedin.datahub-gma:restli-resources-data-template:$gmaVersion",
    'gmaTestModels': "com.linkedin.datahub-gma:test-models:$gmaVersion",
    'gmaTestModelsDataTemplate': "com.linkedin.datahub-gma:test-models-data-template:$gmaVersion",
    'gmaValidators': "com.linkedin.datahub-gma:validators:$gmaVersion",
    'graphqlJava': 'com.graphql-java:graphql-java:16.1',
    'gson': 'com.google.code.gson:gson:2.8.6',
    'guice': 'com.google.inject:guice:4.2.2',
    'guava': 'com.google.guava:guava:27.0.1-jre',
    'h2': 'com.h2database:h2:1.4.196',
    'hibernateCore': 'org.hibernate:hibernate-core:5.2.16.Final',
    'httpClient': 'org.apache.httpcomponents:httpclient:4.5.9',
    'jacksonCore': 'com.fasterxml.jackson.core:jackson-core:2.9.7',
    'jacksonDataBind': 'com.fasterxml.jackson.core:jackson-databind:2.9.7',
    'javatuples': 'org.javatuples:javatuples:1.2',
    'javaxInject' : 'javax.inject:javax.inject:1',
    'jerseyCore': 'org.glassfish.jersey.core:jersey-client:2.25.1',
    'jerseyGuava': 'org.glassfish.jersey.bundles.repackaged:jersey-guava:2.25.1',
    'jsonSimple': 'com.googlecode.json-simple:json-simple:1.1.1',
    'junitJupiterApi': "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion",
    'junitJupiterParams': "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion",
    'junitJupiterEngine': "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion",
    // avro-serde includes dependencies for `kafka-avro-serializer` `kafka-schema-registry-client` and `avro`
    'kafkaAvroSerde': 'io.confluent:kafka-streams-avro-serde:5.5.1',
    'kafkaClients': 'org.apache.kafka:kafka-clients:2.3.0',
    'logbackClassic': 'ch.qos.logback:logback-classic:1.2.3',
    'lombok': 'org.projectlombok:lombok:1.18.12',
    'mariadbConnector': 'org.mariadb.jdbc:mariadb-java-client:2.6.0',
    'mockito': 'org.mockito:mockito-core:3.0.0',
    'mysqlConnector': 'mysql:mysql-connector-java:5.1.47',
    'neo4jHarness': 'org.neo4j.test:neo4j-harness:3.4.11',
    'neo4jJavaDriver': 'org.neo4j.driver:neo4j-java-driver:4.0.1',
    'parseqTest': 'com.linkedin.parseq:parseq:3.0.7:test',
    'picocli': 'info.picocli:picocli:4.5.0',
    'playDocs': 'com.typesafe.play:play-docs_2.11:2.6.18',
    'playGuice': 'com.typesafe.play:play-guice_2.11:2.6.18',
    'playJavaJdbc': 'com.typesafe.play:play-java-jdbc_2.11:2.6.18',
    'playTest': 'com.typesafe.play:play-test_2.11:2.6.18',
    'postgresql': 'org.postgresql:postgresql:42.2.14',
    'reflections': 'org.reflections:reflections:0.9.11',
    'rythmEngine': 'org.rythmengine:rythm-engine:1.3.0',
    'servletApi': 'javax.servlet:javax.servlet-api:3.1.0',
    'springBeans': 'org.springframework:spring-beans:5.2.3.RELEASE',
    'springContext': 'org.springframework:spring-context:5.2.3.RELEASE',
    'springCore': 'org.springframework:spring-core:5.2.3.RELEASE',
    'springJdbc': 'org.springframework:spring-jdbc:5.2.3.RELEASE',
    'springWeb': 'org.springframework:spring-web:5.2.3.RELEASE',
    'springBootAutoconfigure': 'org.springframework.boot:spring-boot-autoconfigure:2.1.2.RELEASE',
    'springBootStarterWeb': 'org.springframework.boot:spring-boot-starter-web:2.1.2.RELEASE',
    'springBootStarterJetty': 'org.springframework.boot:spring-boot-starter-jetty:2.1.2.RELEASE',
    'springKafka': 'org.springframework.kafka:spring-kafka:2.2.14.RELEASE',
    'springActuator': 'org.springframework.boot:spring-boot-starter-actuator:2.1.2.RELEASE',
    'testng': 'org.testng:testng:6.9.9',
    'zookeeper': 'org.apache.zookeeper:zookeeper:3.4.14'
]

allprojects {
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: 'checkstyle'
}

subprojects {
  apply plugin: 'maven'

  plugins.withType(JavaPlugin) {
    dependencies {
      testCompile externalDependency.testng
    }

    tasks.withType(Test) {
      if (!name.startsWith('integ')) {
        useTestNG()
      }
    }

    checkstyle {
      configDirectory = file("${project.rootDir}/gradle/checkstyle")
      sourceSets = [ getProject().sourceSets.main, getProject().sourceSets.test ]
      toolVersion = "8.0"
      maxWarnings = 0
      ignoreFailures = false
    }
  }

  afterEvaluate {
    if (project.plugins.hasPlugin('java')) {
      sourceCompatibility = 1.8
      targetCompatibility = 1.8
    }
  }

  afterEvaluate {
    if (project.plugins.hasPlugin('pegasus')) {
      dependencies {
        dataTemplateCompile spec.product.pegasus.data
        restClientCompile spec.product.pegasus.restliClient
      }
    }
  }
}
