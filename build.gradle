plugins {
  id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'idea'
apply plugin: 'license'
ext.licenseFile = file('.license_header.txt')

version = '1.0'

configurations.all {
  exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  exclude group: 'log4j'

  resolutionStrategy {
      dependencySubstitution {
          substitute module('org.slf4j:slf4j-log4j12') with module('ch.qos.logback:logback-classic:1.1.7')
          //prefer 'log4j-over-slf4j' over 'log4j'
      }
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: 'com.github.hierynomus.license'

  eclipse {
    classpath {
      file {
        // work around for eclipse plugin bug
        whenMerged { classpath ->
          classpath.entries.removeAll { it.path.endsWith('pom') }
        }
      }
    }
  }

  license {
    header licenseFile

    exclude "**/*.scala.html"
    exclude "**/jython/requests/**"
    exclude "**/pyparsing.py"
    excludes(["**/*.json", "**/*.avsc", "**/*.avro", "**/*.conf", "**/*.yaml", "**/*.xml"])
    excludes(["**/*.txt", "**/*.csv"])
  }

  repositories {
    flatDir {
      dirs 'extralibs'
    }
    mavenCentral()
    jcenter()
    maven {
      name "typesafe-maven-release"
      url "https://repo.typesafe.com/typesafe/maven-releases"
    }
    ivy {
      name "typesafe-ivy-release"
      url "https://repo.typesafe.com/typesafe/ivy-releases"
      layout "ivy"
    }
    maven { // this is required for hive
      name "conjars-maven-release"
      url 'http://conjars.org/repo'
    }
    maven { // this is required by confluent kafka common
      name "confluent-maven-release"
      url 'http://packages.confluent.io/maven/'
    }
  }

  task excludegroup(type: Test) {
    useTestNG() {
      excludeGroups 'needConfig'
    }

    testLogging {
      showStandardStreams = true
    }
  }

  ext.externalDependency = ["mysql"               : "mysql:mysql-connector-java:5.1.40",
                            "jython"              : "org.python:jython-standalone:2.7.0",
                            "testng"              : "org.testng:testng:6.9.10",
                            "hadoop_common"       : "org.apache.hadoop:hadoop-common:2.7.3",
                            "hadoop_client"       : "org.apache.hadoop:hadoop-mapreduce-client-core:2.7.3",
                            "hadoop_auth"         : "org.apache.hadoop:hadoop-auth:2.7.3",
                            "hadoop_hdfs"         : "org.apache.hadoop:hadoop-hdfs:2.7.1",
                            "pig"                 : "org.apache.pig:pig:0.15.0",
                            "hive_exec"           : "org.apache.hive:hive-exec:1.2.1",
                            "avro"                : "org.apache.avro:avro:1.7.7",
                            "avro_mapred"         : "org.apache.avro:avro-mapred:1.7.7",
                            "jsch"                : "com.jcraft:jsch:0.1.54",
                            "http_client"         : "org.apache.httpcomponents:httpclient:4.5.2",
                            "http_core"           : "org.apache.httpcomponents:httpcore:4.4.5",
                            "htrace"              : "org.apache.htrace:htrace-core:3.1.0-incubating",
                            "json_path"           : "com.jayway.jsonpath:json-path:2.2.0",
                            "jgit"                : "org.eclipse.jgit:org.eclipse.jgit:4.1.2.201602141800-r",
                            "jsoup"               : "org.jsoup:jsoup:1.8.3",
                            "commons_io"          : "commons-io:commons-io:2.5",

                            "jackson_databind"    : "com.fasterxml.jackson.core:jackson-databind:2.8.4",
                            "jackson_core"        : "com.fasterxml.jackson.core:jackson-core:2.8.4",
                            "jackson_annotations" : "com.fasterxml.jackson.core:jackson-annotations:2.8.4",

                            "slf4j_api"           : "org.slf4j:slf4j-api:1.7.21",
                            "slf4j_log4j"         : "org.slf4j:log4j-over-slf4j:1.7.21",
                            "logback"             : "ch.qos.logback:logback-classic:1.1.7",
                            "jasypt"              : "org.jasypt:jasypt:1.9.2",
                            "mockito"             : "org.mockito:mockito-core:1.10.19",

                            "play"                : "com.typesafe.play:play_2.10:2.4.8",
                            "play_java_jdbc"      : "com.typesafe.play:play-java-jdbc_2.10:2.4.8",
                            "play_java_ws"        : "com.typesafe.play:play-java-ws_2.10:2.4.8",
                            "play_cache"          : "com.typesafe.play:play-cache_2.10:2.4.8",
                            "play_filter"         : "com.typesafe.play:filters-helpers_2.10:2.4.8",
                            "typesafe_config"     : "com.typesafe:config:1.3.1",
                            "netty"               : "io.netty:netty:3.10.6.Final",
                            "akka"                : "com.typesafe.akka:akka-actor_2.10:2.3.15",
                            "spring_jdbc"         : "org.springframework:spring-jdbc:4.1.6.RELEASE",

                            "kafka"               : "org.apache.kafka:kafka_2.10:0.10.0.1",
                            "kafka_clients"       : "org.apache.kafka:kafka-clients:0.10.0.1",
                            "confluent_common_cfg": "io.confluent:common-config:3.0.1"
  ]

  task buildWithWarning(type: JavaCompile, dependsOn: build) {
    compileJava.options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.14'
}
