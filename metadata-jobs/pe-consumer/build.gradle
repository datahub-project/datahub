/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * This file is unmodified from its original version developed by Acryl Data, Inc.,
 * and is now included as part of a repository maintained by the National Digital Twin Programme.
 * All support, maintenance and further development of this code is now the responsibility
 * of the National Digital Twin Programme.
 */

plugins {
  id 'java-library'
  id 'pegasus'
}

apply from: '../../gradle/coverage/java-coverage.gradle'

configurations {
  avro
}

dependencies {
  avro project(path: ':metadata-models', configuration: 'avroSchema')
  implementation project(':li-utils')
  implementation project(':metadata-events:mxe-avro')
  implementation project(':metadata-events:mxe-registration')
  implementation project(':metadata-events:mxe-utils-avro')
  implementation(project(':metadata-service:factories')) {
    exclude group: 'org.neo4j.test'
  }
  implementation externalDependency.springKafka
  implementation externalDependency.slf4jApi
  compileOnly externalDependency.lombok
  annotationProcessor externalDependency.lombok
  runtimeOnly externalDependency.logbackClassic
  testImplementation externalDependency.mockito
  testRuntimeOnly externalDependency.logbackClassic
  testImplementation externalDependency.springBootTest
  testImplementation externalDependency.testng
  testImplementation project(':metadata-operation-context')
}

task avroSchemaSources(type: Copy) {
  dependsOn configurations.avro

  from { // use of closure defers evaluation until execution time
    configurations.avro.collect { zipTree(it) }
  }
  into("src/main/resources/")
  include("avro/com/linkedin/mxe/")
}

compileJava.dependsOn avroSchemaSources
processResources.dependsOn avroSchemaSources

clean {
  delete "src/main/resources/avro"
}