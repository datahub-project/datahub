apply plugin: 'application'
apply plugin: 'java'

configurations {
    avro
}

dependencies {
    avro project(path: ':metadata-models', configuration: 'avroSchema')

    compile project(':li-utils')
    compile project(':metadata-utils')
    compile project(':metadata-builders')
    compile project(':metadata-events:mxe-schemas')
    compile project(':metadata-events:mxe-avro-1.7')
    compile project(':metadata-events:mxe-utils-avro-1.7')
    compile externalDependency.elasticSearchRest

    compile externalDependency.kafkaAvroSerde
    compile externalDependency.kafkaStreams
    compile externalDependency.kafkaSerializers

    compileOnly externalDependency.lombok

    runtime externalDependency.logbackClassic
}

task avroSchemaSources(type: Copy) {
    dependsOn configurations.avro

    from { // use of closure defers evaluation until execution time
        configurations.avro.collect { zipTree(it) }
    }
    into("src/main/resources/")
    include("avro/com/linkedin/mxe/")
}

compileJava.dependsOn avroSchemaSources

clean {
    project.delete("src/main/resources/avro")
}

mainClassName = 'com.linkedin.metadata.kafka.ElasticSearchStreamTask'
