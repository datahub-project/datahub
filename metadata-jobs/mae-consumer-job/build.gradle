plugins {
    id 'org.springframework.boot'
    id 'java'
}

apply plugin: 'pegasus'

configurations {
    avro
}

dependencies {
    avro project(path: ':metadata-models', configuration: 'avroSchema')

    compile project(':li-utils')
    compile (project(':gms:factories')) {
        exclude group: 'org.neo4j.test'
    }
    compile project(':metadata-utils')
    compile project(':metadata-builders')
    compile project(':metadata-events:mxe-schemas')
    compile project(':metadata-events:mxe-avro-1.7')
    compile project(':metadata-events:mxe-registration')
    compile project(':metadata-events:mxe-utils-avro-1.7')

    compile externalDependency.elasticSearchRest
    compile externalDependency.gmaDaoApi
    compile externalDependency.gmaNeo4jDao
    compile externalDependency.kafkaAvroSerde
    compile externalDependency.neo4jJavaDriver

    compile (externalDependency.springBootStarterWeb) {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile externalDependency.springBootStarterJetty
    compile externalDependency.springKafka
    compile externalDependency.springActuator

    compileOnly externalDependency.lombok

    annotationProcessor externalDependency.lombok

    runtime externalDependency.logbackClassic
}

task avroSchemaSources(type: Copy) {
    dependsOn configurations.avro

    from { // use of closure defers evaluation until execution time
        configurations.avro.collect { zipTree(it) }
    }
    into("src/main/resources/")
    include("avro/com/linkedin/mxe/")
}

compileJava.dependsOn avroSchemaSources

clean {
    project.delete("src/main/resources/avro")
}

bootJar {
    mainClassName = 'com.linkedin.metadata.kafka.MaeConsumerApplication'
}
