plugins {
    id 'base'
}

apply from: "../../gradle/coverage/python-coverage.gradle"

ext {
  python_executable = 'python3'
  venv_name = 'venv'
}

ext.venv_activate_command = "set +x && source ${venv_name}/bin/activate && set -x && "

if (!project.hasProperty("extra_pip_requirements")) {
    ext.extra_pip_requirements = ""
}
if (!project.hasProperty("extra_pip_constraints")) {
    ext.extra_pip_constraints = ""
}
if (!project.hasProperty("extra_pip_extras")) {
    ext.extra_pip_extras = ""
}
// If extra_pip_extras is non-empty, we need to add a comma to the beginning of the string.
if (extra_pip_extras != "") {
    ext.extra_pip_extras = "," + extra_pip_extras
}

def pip_install_command = "VIRTUAL_ENV=${venv_name} ${venv_name}/bin/uv pip install -e ../../metadata-ingestion ${extra_pip_requirements}"

task environmentSetup(type: Exec) {
  def sentinel_file = "${venv_name}/.venv_environment_sentinel"
  inputs.file file('setup.py')
  outputs.file(sentinel_file)
  def venv_copies_flag = System.getenv('DATAHUB_VENV_USE_COPIES') == 'true' ? '--copies' : ''
  commandLine 'bash', '-c',
    "${python_executable} -m venv ${venv_copies_flag} ${venv_name} && set -x && " +
    "${venv_name}/bin/python -m pip install --upgrade uv && " +
    "touch ${sentinel_file}"
}

task install(type: Exec, dependsOn: [environmentSetup, ':metadata-ingestion:codegen']) {
  def sentinel_file = "${venv_name}/.build_install_package_sentinel"
  inputs.file file('setup.py')
  outputs.file(sentinel_file)
  commandLine 'bash', '-c',
    venv_activate_command +
    "${pip_install_command} -e .[ignore${extra_pip_extras}] ${extra_pip_constraints} &&" +
    "touch ${sentinel_file}"
}

task installDev(type: Exec, dependsOn: [install]) {
  def sentinel_file = "${venv_name}/.build_install_dev_sentinel"
  inputs.file file('setup.py')
  outputs.file("${sentinel_file}")
  // For some reason, the Airflow constraints files pin things like black and pytest.
  // This allows us to not respect those constraints ourselves.
  commandLine 'bash', '-c',
    venv_activate_command +
    "${pip_install_command} -e .[dev${extra_pip_extras}] && " +
    "touch ${sentinel_file}"
}
task installTest(type: Exec, dependsOn: [installDev]) {
  def sentinel_file = "${venv_name}/.build_install_test_sentinel"
  inputs.file file('setup.py')
  outputs.file(sentinel_file)
  commandLine 'bash', '-c',
    venv_activate_command +
    "${pip_install_command} -e .[integration-tests${extra_pip_extras}] ${extra_pip_constraints} && " +
    "touch ${sentinel_file}"
}

task lint(type: Exec, dependsOn: installDev) {
  commandLine 'bash', '-c',
    venv_activate_command +
    "ruff check src/ tests/ && " +
    "ruff format --check src/ tests/ && " +
    // Detect Airflow version and exclude incompatible plugin implementation
    // This mirrors the tox approach: check only one implementation at a time
    "AIRFLOW_VERSION=\$(python -c 'import airflow; print(airflow.__version__)') && " +
    "if python -c 'import packaging.version; import airflow; exit(0 if packaging.version.parse(airflow.__version__) >= packaging.version.parse(\"3.0.0\") else 1)'; then " +
      "echo \"Detected Airflow \$AIRFLOW_VERSION (3.x) - excluding airflow2 from mypy\" && " +
      "mypy --exclude 'src/datahub_airflow_plugin/airflow2' --show-traceback --show-error-codes src/ tests/; " +
    "else " +
      "echo \"Detected Airflow \$AIRFLOW_VERSION (2.x) - excluding airflow3 from mypy\" && " +
      "mypy --exclude 'src/datahub_airflow_plugin/airflow3' --show-traceback --show-error-codes src/ tests/; " +
    "fi"
}
task lintFix(type: Exec, dependsOn: installDev) {
  commandLine 'bash', '-c',
    venv_activate_command +
    "ruff check --fix src/ tests/ && " +
    "ruff format src/ tests/ "
}

// HACK: Some of the Airflow constraint files conflict with packages that we install (e.g. black).
// I'm not sure why they constrain those in the first place, but we don't want to respect them
// when running linting. However, we do want to respect them when running integration tests.
// There's two install steps, and these constraints ensure that we run them in the right order.
installTest.shouldRunAfter lint
installTest.shouldRunAfter lintFix

task testFull(type: Exec, dependsOn: installTest) {
  inputs.files(project.fileTree(dir: "src/", include: "**/*.py"))
  inputs.files(project.fileTree(dir: "tests/"))
  inputs.file file("pyproject.toml")
  inputs.file file("setup.cfg")
  inputs.file file("setup.py")
  commandLine 'bash', '-c',
    venv_activate_command +
    "pytest ${get_coverage_args('quick')} -vv --continue-on-collection-errors"
}


task cleanPythonCache(type: Exec) {
  commandLine 'bash', '-c',
    "find src -type f -name '*.py[co]' -delete -o -type d -name __pycache__ -delete -o -type d -empty -delete"
}
task buildWheel(type: Exec, dependsOn: [environmentSetup]) {
  commandLine 'bash', '-c',
    venv_activate_command +
    'uv pip install build && RELEASE_VERSION="\${RELEASE_VERSION:-0.0.0.dev1}" RELEASE_SKIP_INSTALL=1 RELEASE_SKIP_UPLOAD=1 ./scripts/release.sh'
}

build.dependsOn install
check.dependsOn lint
check.dependsOn testFull

clean {
  delete venv_name
  delete 'build'
  delete 'dist'
  delete '.ruff_cache'
  delete '.mypy_cache'
  delete '.pytest_cache'
}
clean.dependsOn cleanPythonCache
