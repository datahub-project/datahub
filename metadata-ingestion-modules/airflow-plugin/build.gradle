plugins {
    id 'base'
}

ext {
  python_executable = 'python3'
  venv_name = 'venv'
}

if (!project.hasProperty("extra_pip_requirements")) {
    ext.extra_pip_requirements = ""
}

def pip_install_command = "${venv_name}/bin/pip install -e ../../metadata-ingestion"

task checkPythonVersion(type: Exec) {
  commandLine python_executable, '-c', 'import sys; assert sys.version_info >= (3, 7)'
}

task environmentSetup(type: Exec, dependsOn: checkPythonVersion) {
  def sentinel_file = "${venv_name}/.venv_environment_sentinel"
  inputs.file file('setup.py')
  outputs.file(sentinel_file)
  commandLine 'bash', '-c', 
    "${python_executable} -m venv ${venv_name} &&" +
    "${venv_name}/bin/python -m pip install --upgrade pip wheel 'setuptools>=63.0.0' && " +
    "touch ${sentinel_file}"
}

task installPackage(type: Exec, dependsOn: [environmentSetup, ':metadata-ingestion:codegen']) {
  def sentinel_file = "${venv_name}/.build_install_package_sentinel"
  inputs.file file('setup.py')
  outputs.file(sentinel_file)
  // Workaround for https://github.com/yaml/pyyaml/issues/601.
  // See https://github.com/yaml/pyyaml/issues/601#issuecomment-1638509577.
  // and https://github.com/datahub-project/datahub/pull/8435.
  commandLine 'bash', '-x', '-c',
    "${pip_install_command} install 'Cython<3.0' 'PyYAML<6' --no-build-isolation && " +
    "${pip_install_command} -e . ${extra_pip_requirements} &&" +
    "touch ${sentinel_file}"
}

task install(dependsOn: [installPackage])

task installDev(type: Exec, dependsOn: [install]) {
  def sentinel_file = "${venv_name}/.build_install_dev_sentinel"
  inputs.file file('setup.py')
  outputs.file("${sentinel_file}")
  commandLine 'bash', '-x', '-c',
    "${pip_install_command} -e .[dev]  ${extra_pip_requirements} && " +
    "touch ${sentinel_file}"
}

task lint(type: Exec, dependsOn: installDev) {
  /*
    The find/sed combo below is a temporary work-around for the following mypy issue with airflow 2.2.0:
   "venv/lib/python3.8/site-packages/airflow/_vendor/connexion/spec.py:169: error: invalid syntax".
   */
  commandLine 'bash', '-c',
    "find ${venv_name}/lib -path *airflow/_vendor/connexion/spec.py -exec sed -i.bak -e '169,169s/  # type: List\\[str\\]//g' {} \\; && " +
    "source ${venv_name}/bin/activate && set -x && " +
    "black --check --diff src/ tests/ && " +
    "isort --check --diff src/ tests/ && " +
    "flake8 --count --statistics src/ tests/ && " +
    "mypy --show-traceback --show-error-codes src/ tests/"
}
task lintFix(type: Exec, dependsOn: installDev) {
  commandLine 'bash', '-x', '-c',
    "source ${venv_name}/bin/activate && " +
    "black src/ tests/ && " +
    "isort src/ tests/ && " +
    "flake8 src/ tests/ && " +
    "mypy src/ tests/ "
}

task installDevTest(type: Exec, dependsOn: [installDev]) {
  def sentinel_file = "${venv_name}/.build_install_dev_test_sentinel"
  inputs.file file('setup.py')
  outputs.dir("${venv_name}")
  outputs.file("${sentinel_file}")
  commandLine 'bash', '-x', '-c',
    "${pip_install_command} -e .[dev,integration-tests] && touch ${sentinel_file}"
}

def testFile = hasProperty('testFile') ? testFile : 'unknown'
task testSingle(dependsOn: [installDevTest]) {
  doLast {
  if (testFile != 'unknown')  {
  exec {
    commandLine 'bash', '-x', '-c',
     "source ${venv_name}/bin/activate && pytest ${testFile}"
    }
  } else {
   throw new GradleException("No file provided. Use -PtestFile=<test_file>")
    }
  }
}

task testQuick(type: Exec, dependsOn: installDevTest) {
  // We can't enforce the coverage requirements if we run a subset of the tests.
  inputs.files(project.fileTree(dir: "src/", include: "**/*.py"))
  inputs.files(project.fileTree(dir: "tests/"))
  outputs.dir("${venv_name}")
  commandLine 'bash', '-x', '-c',
    "source ${venv_name}/bin/activate && pytest -vv --continue-on-collection-errors --junit-xml=junit.quick.xml"
}


task testFull(type: Exec, dependsOn: [testQuick, installDevTest]) {
  commandLine 'bash', '-x', '-c',
    "source ${venv_name}/bin/activate && pytest -m 'not slow_integration' -vv --continue-on-collection-errors --junit-xml=junit.full.xml"
}
task buildWheel(type: Exec, dependsOn: [install]) {
  commandLine 'bash', '-c', "source ${venv_name}/bin/activate && " + 'pip install build && RELEASE_VERSION="\${RELEASE_VERSION:-0.0.0.dev1}" RELEASE_SKIP_TEST=1 RELEASE_SKIP_UPLOAD=1 ./scripts/release.sh'
}

task cleanPythonCache(type: Exec) {
  commandLine 'bash', '-c',
    "find src -type f -name '*.py[co]' -delete -o -type d -name __pycache__ -delete -o -type d -empty -delete"
}

build.dependsOn install
check.dependsOn lint
check.dependsOn testQuick

clean {
  delete venv_name
  delete 'build'
  delete 'dist'
}
clean.dependsOn cleanPythonCache
