apply plugin: 'java'
apply plugin: 'org.hidetake.swagger.generator'

dependencies {
// Dependencies for open api
    implementation externalDependency.reflections
    implementation externalDependency.springBoot
    implementation externalDependency.springCore
    implementation externalDependency.springWeb
    implementation externalDependency.springWebMVC
    implementation externalDependency.springBeans
    implementation externalDependency.springContext
    implementation externalDependency.antlr4Runtime
    implementation externalDependency.antlr4
    implementation externalDependency.javaxValidation
    implementation externalDependency.servletApi
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation externalDependency.jacksonDataBind
    implementation externalDependency.slf4jApi

    // End of dependencies
    implementation externalDependency.swaggerAnnotations
    swaggerCodegen externalDependency.swaggerCli

    testImplementation externalDependency.assertJ
}

tasks.register('generateOpenApiPojos', GenerateSwaggerCode) {
    it.setInputFile(
        file(
            "$projectDir/src/main/resources/schema-registry-open-api-v7.4.0.yaml"
        )
    )
    it.setOutputDir(file("$projectDir/generated"))
    it.setLanguage("spring")
    it.setComponents([models: true, apis: true, apiTests: false])
    it.setTemplateDir(file("$projectDir/src/main/resources/JavaSpring"))
    it.setAdditionalProperties([
        "group-id" : "io.datahubproject",
        "dateLibrary" : "java8",
        "java11" : "true",
        "modelPropertyNaming" : "original",
        "modelPackage" : "io.datahubproject.schema-registry.openapi.generated"] as Map<String, String>)
}

sourceSets.main.java.srcDirs "${generateOpenApiPojos.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${generateOpenApiPojos.outputDir}/src/main/resources"
