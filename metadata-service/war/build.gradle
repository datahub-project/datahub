plugins {
  id 'war'
  id 'com.palantir.docker'
}

apply from: "../../gradle/versioning/versioning.gradle"

ext {
  docker_repo = 'datahub-gms'
}

ext.apiProject = project(':metadata-service:restli-api')

dependencies {
  runtime project(':metadata-service:factories')
  runtime project(':metadata-service:auth-filter')
  runtime project(':metadata-service:servlet')
  runtime project(':metadata-service:auth-servlet-impl')
  runtime project(':metadata-service:graphql-servlet-impl')
  runtime project(':metadata-service:openapi-servlet')
  runtime project(':metadata-jobs:mce-consumer')
  runtime project(':metadata-jobs:mae-consumer')
  runtime project(':metadata-jobs:pe-consumer')

  runtime externalDependency.awsSecretsManagerJdbc
  runtime externalDependency.h2
  runtime externalDependency.mariadbConnector
  runtime externalDependency.mysqlConnector
  runtime externalDependency.postgresql
  runtime externalDependency.springWebMVC

  runtime spec.product.pegasus.restliDocgen
  runtime spec.product.pegasus.restliSpringBridge

  runtime externalDependency.log4jCore
  runtime externalDependency.log4j2Api
  runtime externalDependency.logbackClassic
  implementation externalDependency.awsMskIamAuth
}

configurations {
  jetty9
}

dependencies {
  jetty9 "org.eclipse.jetty:jetty-runner:9.4.46.v20220331"
}

task run(type: JavaExec, dependsOn: build) {
  main = "org.eclipse.jetty.runner.Runner"
  args = ["${projectDir}/build/libs/war.war"]
  classpath configurations.jetty9
}

war {
  archiveFileName = "war.war"
}

docker {
  name "${docker_registry}/${docker_repo}:v${version}"
  version "v${version}"
  dockerfile file("${rootProject.projectDir}/docker/${docker_repo}/Dockerfile")
  files war.outputs.files
  files fileTree(rootProject.projectDir) {
    include 'docker/monitoring/*'
    include "docker/${docker_repo}/*"
    include 'metadata-models/src/main/resources/*'
  }
  tag("Debug", "${docker_registry}/${docker_repo}:debug")

  // platform('linux/arm64', 'linux/amd64')
  buildx(true)
  load(true)
  push(false)
}
tasks.getByName("docker").dependsOn([build, war])

task cleanLocalDockerImages {
  doLast {
    rootProject.ext.cleanLocalDockerImages(docker_registry, docker_repo, "v${version}")
  }
}
dockerClean.finalizedBy(cleanLocalDockerImages)