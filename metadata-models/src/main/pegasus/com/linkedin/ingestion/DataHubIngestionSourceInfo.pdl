namespace com.linkedin.ingestion

import com.linkedin.common.AuditStamp
import com.linkedin.common.Urn

/**
 * Info about a DataHub ingestion source
 */
@Aspect = {
  "name": "dataHubIngestionSourceInfo"
}
record DataHubIngestionSourceInfo {
  /**
   * The display name of the ingestion source
   */
  @Searchable = {
   "fieldType": "TEXT_PARTIAL"
  }
  name: string

  /**
   * The type of the source itself, e.g. mysql, bigquery, bigquery-usage. Should match the recipe.
   */
  @Searchable = {
   "fieldType": "KEYWORD",
   "queryByDefault": false
  }
  type: string

  /**
   * Data Platform URN associated with the source
   */
  platform: optional Urn

  /**
  * The schedule on which the ingestion source is executed
  */
  schedule: optional DataHubIngestionSourceSchedule

  /**
  * Parameters associated with the Ingestion Source
  */
  config: record DataHubIngestionSourceConfig {
     /**
      * The JSON recipe to use for ingestion
      */
      recipe: string

     /**
      * The PyPI version of the datahub CLI to use when executing a recipe
      */
      version: optional string

     /**
      * The id of the executor to use to execute the ingestion run
      */
      @Searchable = {
         "fieldName": "sourceExecutorId",
         "fieldType": "KEYWORD",
         "queryByDefault": false
      }
      executorId: optional string

     /**
      * Whether or not to run this ingestion source in debug mode
      */
      debugMode: optional boolean

     /**
      * Extra arguments for the ingestion run.
      */
      extraArgs: optional map[string, string]
  }

  /**
   * The source or origin of the Ingestion Source
   *
   * Currently CLI and UI do not provide an explicit source.
   */
   source: optional record DataHubIngestionSourceSource {
        /**
         * The source type of the ingestion source
         */
        @Searchable = {
           "fieldName": "sourceType",
           "fieldType": "KEYWORD",
           "queryByDefault": false
        }
        type: enum DataHubIngestionSourceSourceType {
            /**
             * A system internal source, e.g. for running search indexing operations, feature computation, etc.
             */
            SYSTEM
        }
   }
}
