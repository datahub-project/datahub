namespace com.linkedin.assertion

import com.linkedin.common.Urn

/**
* Assertion attributes that are applicable to Dataset Assertions
**/
record DatasetAssertionInfo {
    /**
    * Scope of the Assertion. What part of the dataset does this assertion apply to?
    * Declared optional to make it convenient to inline into other aspects like AssertionInfo without requiring
    * additional nesting. Semantically required, if this is a Dataset Assertion.
    **/
    datasetAssertionScope: optional enum DatasetAssertionScope {
      /**
      * This assertion applies to dataset columns
      */
      DATASET_COLUMN
      /**
      * This assertion applies to entire rows of the dataset
      */
      DATASET_ROWS
      /**
      * This assertion applies to the schema of the dataset
      */
      DATASET_SCHEMA
      // Future evolution can include things like CROSS_DATASET assertions
    }

    /**
    * Assertion details when scope is DATASET_COLUMN
    */
    datasetColumnAssertion: optional DatasetColumnAssertion

    /**
    * Assertion details when scope is DATASET_ROWS
    */
    datasetRowsAssertion: optional DatasetRowsAssertion

    /**
    * Assertion details when scope is DATASET_SCHEMA
    */
    datasetSchemaAssertion: optional DatasetSchemaAssertion

    /**
    * One or more dataset schema fields that are targeted by this assertion
    */
    @Relationship = {
      "/*": {
        "name": "Asserts",
        "entityTypes": [ "schemaField" ]
      }
    }
    datasetFields: optional array[Urn]

    /**
    * One or more datasets that are targeted by this assertion
    */
    @Relationship = {
      "/*": {
        "name": "Asserts",
        "entityTypes": [ "dataset" ]
      }
    }
    datasets: optional array[Urn]
}