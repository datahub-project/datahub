namespace com.linkedin.glossary

import com.linkedin.common.Url
import com.linkedin.common.GlossaryNodeUrn
import com.linkedin.common.CustomProperties
import com.linkedin.schema.PrimitiveValueDataType

/**
 * Properties associated with a GlossaryTerm
 */
@Aspect = {
   "name": "glossaryTermInfo"
}
record GlossaryTermInfo includes CustomProperties {
  /**
   * Optional id for the term
   */
  @Searchable = {
    "fieldType": "TEXT_PARTIAL",
  }
  id: optional string

  /**
   * Display name of the term
   */
  @Searchable = {
    "fieldType": "WORD_GRAM",
    "enableAutocomplete": true,
    "boostScore": 10.0,
    "fieldNameAliases": [ "_entityName" ],
    "searchTier": 1,
    "searchLabel": "entityName"
  }
  name: optional string

  /**
   * Definition of business term.
   */
  @Searchable = {}
  definition: string

  /**
   * Parent node of the glossary term
   */
  @Relationship = {
    "name": "IsPartOf",
    "entityTypes": [ "glossaryNode" ],
  }
  @Searchable = {
    "fieldName": "parentNode",
    "fieldType": "URN",
    "hasValuesFieldName": "hasParentNode"
  }
  parentNode: optional GlossaryNodeUrn

  /**
   * Source of the Business Term (INTERNAL or EXTERNAL) with default value as INTERNAL
   */
  @Searchable = {
    "fieldType": "KEYWORD"
  }
  termSource: string

  /**
   * External Reference to the business-term
   */
  @Searchable = {
    "fieldType": "KEYWORD"
  }
  sourceRef: optional string

  /**
   * The abstracted URL such as https://spec.edmcouncil.org/fibo/ontology/FBC/FinancialInstruments/FinancialInstruments/CashInstrument.
   */
  sourceUrl: optional Url

  /**
   * Schema definition of the glossary term
   */
  @deprecated
  rawSchema: optional string

}
