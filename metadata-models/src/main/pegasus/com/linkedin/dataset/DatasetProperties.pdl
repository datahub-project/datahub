namespace com.linkedin.dataset

import com.linkedin.common.Uri
import com.linkedin.common.CustomProperties
import com.linkedin.common.ExternalReference
import com.linkedin.common.TimeStamp

/**
 * Properties associated with a Dataset
 */
@Aspect = {
  "name": "datasetProperties"
}
record DatasetProperties includes CustomProperties, ExternalReference {

  /**
   * Display name of the Dataset
   */
  @Searchable = {
    "fieldType": "WORD_GRAM",
    "enableAutocomplete": true,
    "boostScore": 10.0,
    "fieldNameAliases": [ "_entityName" ],
    "searchTier": 1,
    "searchLabel": "entityName"
  }
  name: optional string

  /**
   * Fully-qualified name of the Dataset
   */
  @Searchable = {
    "fieldType": "WORD_GRAM",
    "addToFilters": false,
    "enableAutocomplete": true,
    "boostScore": 10.0,
    "searchTier": 1,
    "searchLabel": "qualifiedName"
  }
  qualifiedName: optional string

  /**
   * Documentation of the dataset
   */
  @Searchable = {
    "fieldType": "TEXT",
    "hasValuesFieldName": "hasDescription",
    "searchTier": 2
  }
  description: optional string

  /**
   * The abstracted URI such as hdfs:///data/tracking/PageViewEvent, file:///dir/file_name. Uri should not include any environment specific properties. Some datasets might not have a standardized uri, which makes this field optional (i.e. kafka topic).
   */
  @deprecated = "Use ExternalReference.externalUrl field instead."
  uri: optional Uri

  /**
   * A timestamp documenting when the asset was created in the source Data Platform (not on DataHub)
   */
  @Searchable = {
    "/time": {
      "fieldName": "createdAt",
      "fieldType": "DATETIME",
      "searchLabel": "createdAt"
    }
  }
  created: optional TimeStamp

  /**
   * A timestamp documenting when the asset was last modified in the source Data Platform (not on DataHub)
   */
  @Searchable = {
    "/time": {
      "fieldName": "lastModifiedAt",
      "fieldType": "DATETIME",
      "searchLabel": "lastModifiedAt"
    }
  }
  lastModified: optional TimeStamp

  /**
   * [Legacy] Unstructured tags for the dataset. Structured tags can be applied via the `GlobalTags` aspect.
   * This is now deprecated.
   */
  @deprecated = "Use GlobalTags aspect instead."
  tags: array[string] = [ ]
}
