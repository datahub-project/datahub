namespace com.linkedin.common

import com.linkedin.versionset.VersioningScheme

/**
 * Properties about a versioned asset i.e. dataset, ML Model, etc.
 */
@Aspect = {
  "name": "versionProperties"
}
record VersionProperties {
  /**
   * The linked Version Set entity that ties multiple versioned assets together
   */
  @Searchable = {
    "queryByDefault": false
  }
  @Relationship = {
    "name": "VersionOf",
    "entityTypes": [ "versionSet" ]
  }
  versionSet: Urn

  /**
   * Label for this versioned asset, is unique within a version set
   */
  @Searchable = {
    "/versionTag": {
      "fieldName": "version",
      "queryByDefault": false
    }
  }
  version: VersionTag

  /**
   * Associated aliases for this versioned asset
   */
  @Searchable = {
    "/*/versionTag": {
      "fieldName": "aliases",
      "queryByDefault": false
    }
  }
  aliases: array[VersionTag] = []

  /**
   * Comment documenting what this version was created for, changes, or represents
   */
  comment: optional string

  /**
   * Sort identifier that determines where a version lives in the order of the Version Set.
   * What this looks like depends on the Version Scheme. For sort ids generated by DataHub we use an 8 character string representation.
   */
  @Searchable = {
    "queryByDefault": false,
    "fieldName": "versionSortId"
  }
  sortId: string

  /**
   * What versioning scheme `sortId` belongs to.
   * Defaults to a plain string that is lexicographically sorted.
   */
  versioningScheme: VersioningScheme = "LEXICOGRAPHIC_STRING"

  /**
   * Timestamp reflecting when this asset version was created in the source system.
   */
  sourceCreatedTimestamp: optional AuditStamp

  /**
   * Timestamp reflecting when the metadata for this version was created in DataHub
   */
  metadataCreatedTimestamp: optional AuditStamp

  /**
   * Marks whether this version is currently the latest. Set by a side effect and should not be modified by API.
   */
   @Searchable = {
       "queryByDefault": false,
       "fieldType": "BOOLEAN"
   }
  isLatest: optional boolean
}
