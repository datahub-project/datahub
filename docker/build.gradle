plugins {
    id 'java' // required by versioning
}

apply from: "../gradle/versioning/versioning.gradle"

ext {
    quickstart_modules = [
            ':docker:elasticsearch-setup',
            ':docker:mysql-setup',
            ':docker:kafka-setup',
            ':datahub-upgrade',
            ':metadata-jobs:mce-consumer-job',
            ':metadata-jobs:mae-consumer-job',
            ':metadata-service:war',
            ':datahub-frontend',
    ]

    debug_modules = quickstart_modules - [':metadata-jobs:mce-consumer-job',
                                          ':metadata-jobs:mae-consumer-job']
    debug_compose_args = [
            '-f', 'docker-compose-without-neo4j.yml',
            '-f', 'docker-compose-without-neo4j.override.yml',
            '-f', 'docker-compose-without-neo4j.m1.yml', // updates to mariadb
            '-f', 'docker-compose.dev.yml'
    ]
    debug_reloadable = [
            'datahub-gms',
            'datahub-frontend-react'
    ]

    // Postgres
    pg_quickstart_modules = quickstart_modules - [':docker:mysql-setup'] + [':docker:postgres-setup']
    pg_compose_args = [
            '-f', 'docker-compose-without-neo4j.yml',
            '-f', 'docker-compose-without-neo4j.postgres.override.yml'
    ]
}

task quickstart(type: Exec, dependsOn: ':metadata-ingestion:install') {
    dependsOn(quickstart_modules.collect { it + ':dockerTag' })
    shouldRunAfter ':metadata-ingestion:clean', 'quickstartNuke'

    environment "DATAHUB_TELEMETRY_ENABLED", "false"
    environment "DOCKER_COMPOSE_BASE", "file://${rootProject.projectDir}"
    // environment "ACTIONS_VERSION", 'alpine3.18-slim'
    // environment "DATAHUB_ACTIONS_IMAGE", 'nginx'

    // Elastic
    // environment "DATAHUB_SEARCH_IMAGE", 'elasticsearch'
    // environment "DATAHUB_SEARCH_TAG", '7.10.1'

    // OpenSearch
    environment "DATAHUB_SEARCH_IMAGE", 'opensearchproject/opensearch'
    environment "DATAHUB_SEARCH_TAG", '2.9.0'
    environment "XPACK_SECURITY_ENABLED", 'plugins.security.disabled=true'
    environment "USE_AWS_ELASTICSEARCH", 'true'

    def cmd = [
            'source ../metadata-ingestion/venv/bin/activate && ',
            'datahub docker quickstart',
            '--no-pull-images',
            '--standalone_consumers',
            '--version', "v${version}",
            '--dump-logs-on-failure'
    ]

    commandLine 'bash', '-c', cmd.join(" ")
}

task quickstartSlim(type: Exec, dependsOn: ':metadata-ingestion:install') {
    dependsOn(([':docker:datahub-ingestion'] + quickstart_modules).collect { it + ':dockerTag' })
    shouldRunAfter ':metadata-ingestion:clean', 'quickstartNuke'

    environment "DATAHUB_TELEMETRY_ENABLED", "false"
    environment "DOCKER_COMPOSE_BASE", "file://${rootProject.projectDir}"
    environment "DATAHUB_ACTIONS_IMAGE", "acryldata/datahub-ingestion"
    environment "ACTIONS_VERSION", "v${version}-slim"
    environment "ACTIONS_EXTRA_PACKAGES", 'acryl-datahub-actions[executor] acryl-datahub-actions'
    environment "ACTIONS_CONFIG", 'https://raw.githubusercontent.com/acryldata/datahub-actions/main/docker/config/executor.yaml'

    def cmd = [
            'source ../metadata-ingestion/venv/bin/activate && ',
            'datahub docker quickstart',
            '--no-pull-images',
            '--standalone_consumers',
            '--version', "v${version}",
            '--dump-logs-on-failure'
    ]

    commandLine 'bash', '-c', cmd.join(" ")
}

task quickstartNuke(type: Exec, dependsOn: ":metadata-ingestion:install") {
    shouldRunAfter(':metadata-ingestion:clean')

    def cmd = [
            'source ../metadata-ingestion/venv/bin/activate && ',
            'datahub docker nuke'
    ]
    commandLine 'bash', '-c',  cmd.join(" ")
}

task quickstartDebug(type: Exec, dependsOn: ':metadata-ingestion:install') {
    dependsOn(debug_modules.collect { it + ':dockerTagDebug' })
    shouldRunAfter ':metadata-ingestion:clean', 'quickstartNuke'

    environment "DATAHUB_TELEMETRY_ENABLED", "false"
    environment "DOCKER_COMPOSE_BASE", "file://${rootProject.projectDir}"

    // Elastic
    // environment "DATAHUB_SEARCH_IMAGE", 'elasticsearch'
    // environment "DATAHUB_SEARCH_TAG", '7.10.1'

    // OpenSearch
    environment "DATAHUB_SEARCH_IMAGE", 'opensearchproject/opensearch'
    environment "DATAHUB_SEARCH_TAG", '2.9.0'
    environment "XPACK_SECURITY_ENABLED", 'plugins.security.disabled=true'
    environment "USE_AWS_ELASTICSEARCH", 'true'


    def cmd = [
            'source ../metadata-ingestion/venv/bin/activate && ',
            'datahub docker quickstart',
            '--no-pull-images',
            '--version', "debug",
            '--dump-logs-on-failure'
    ] + debug_compose_args
    commandLine 'bash', '-c', cmd.join(" ")
}

task debugReload(type: Exec) {
    def cmd = ['docker compose -p datahub'] + debug_compose_args + ['restart'] + debug_reloadable
    commandLine 'bash', '-c', cmd.join(" ")
}

task quickstartPg(type: Exec, dependsOn: ':metadata-ingestion:install') {
    dependsOn(pg_quickstart_modules.collect { it + ':dockerTag' })
    shouldRunAfter ':metadata-ingestion:clean', 'quickstartNuke'

    environment "DATAHUB_TELEMETRY_ENABLED", "false"
    environment "DOCKER_COMPOSE_BASE", "file://${rootProject.projectDir}"
    environment "DATAHUB_POSTGRES_VERSION", "15.5"

    // OpenSearch
    environment "DATAHUB_SEARCH_IMAGE", 'opensearchproject/opensearch'
    environment "DATAHUB_SEARCH_TAG", '2.9.0'
    environment "XPACK_SECURITY_ENABLED", 'plugins.security.disabled=true'
    environment "USE_AWS_ELASTICSEARCH", 'true'

    def cmd = [
            'source ../metadata-ingestion/venv/bin/activate && ',
            'datahub docker quickstart',
            '--no-pull-images',
            '--standalone_consumers',
            '--version', "v${version}",
            '--dump-logs-on-failure'
    ] + pg_compose_args

    commandLine 'bash', '-c', cmd.join(" ")
}