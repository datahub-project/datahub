plugins {
    id 'java' // required for versioning
}

apply from: "../../gradle/versioning/versioning.gradle"
apply from: "../../gradle/docker/docker.gradle"

ext {
    docker_registry = rootProject.ext.docker_registry == 'linkedin' ? 'acryldata' : docker_registry
    docker_repo = 'datahub-ingestion'
    docker_dir = 'datahub-ingestion'
    docker_target = project.getProperties().getOrDefault("dockerTarget", "slim")
    docker_version = "v${version}${docker_target == 'slim' ? '-slim' : ''}"

    revision = 12 // increment to trigger rebuild
}

dependencies {
    project(':metadata-ingestion')
}

docker {
    dependsOn 'build', ':metadata-ingestion:codegen'
    name "${docker_registry}/${docker_repo}:${docker_version}"
    dockerfile file("${rootProject.projectDir}/docker/${docker_dir}/Dockerfile")
    files fileTree(rootProject.projectDir) {
        include '.dockerignore'
        include "docker/${docker_dir}/*"
        include "metadata-ingestion/**"
        include "metadata-ingestion-modules/**"
        include "python-build/**"
    }.exclude {
        i -> (!i.file.name.endsWith(".dockerignore") && i.file.isHidden())
    }

    version "${docker_version}"
    // This task is intended to build the slim image
    target 'ingestion-base-slim'
    def dockerBuildArgs = [RELEASE_VERSION: version.replace('-SNAPSHOT', '').replace('v', '').replace("-slim", '')]

    // Add build args if they are defined (needed for some CI or enterprise environments)
    if (project.hasProperty('pipMirrorUrl')) {
        dockerBuildArgs.PIP_MIRROR_URL = project.getProperty('pipMirrorUrl')
    }
    if (project.hasProperty('debianAptRepositoryUrl')) {
        dockerBuildArgs.DEBIAN_REPO_URL = project.getProperty('debianAptRepositoryUrl')
    }

    buildArgs(dockerBuildArgs)
}
