/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * This file is unmodified from its original version developed by Acryl Data, Inc.,
 * and is now included as part of a repository maintained by the National Digital Twin Programme.
 * All support, maintenance and further development of this code is now the responsibility
 * of the National Digital Twin Programme.
 */

plugins {
  id 'java-library'
  id 'pegasus'
}

dependencies {
  dataModel project(path: ':li-utils', configuration: 'dataTemplate')
}

task copyMetadataModels(type: Sync) {
  from("../../metadata-models/src/main/pegasus/")
  into file("src/main/pegasus")
}

generateAvroSchema.dependsOn copyMetadataModels
validateSchemaAnnotation.dependsOn copyMetadataModels
mainTranslateSchemas.dependsOn copyMetadataModels
generateDataTemplate.dependsOn copyMetadataModels
mainCopySchemas.dependsOn copyMetadataModels
pegasus.main.generationModes = [PegasusGenerationMode.PEGASUS, PegasusGenerationMode.AVRO]

task renameNamespace(type: Sync, dependsOn: generateAvroSchema) {
  from("src/mainGeneratedAvroSchema/avro")
  into file("src/renamed/avro")

  doLast {
    project.exec {
      commandLine 'bash', './rename-namespace.sh'
    }
  }
}

build.dependsOn renameNamespace

clean {
  delete 'src/main/pegasus'
  delete 'src/mainGeneratedAvroSchema/avro'
  delete 'src/renamed/avro'
  delete 'bin'
}

// This project only contains generated code, so disable Spotless
tasks.withType(com.diffplug.gradle.spotless.SpotlessTask).configureEach {
  enabled = false
}

