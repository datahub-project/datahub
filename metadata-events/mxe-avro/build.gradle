/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * This file is unmodified from its original version developed by Acryl Data, Inc.,
 * and is now included as part of a repository maintained by the National Digital Twin Programme.
 * All support, maintenance and further development of this code is now the responsibility
 * of the National Digital Twin Programme.
 */

plugins {
  id 'java-library'
  id 'io.acryl.gradle.plugin.avro'
}

configurations {
  avsc
}

dependencies {
  api externalDependency.avro
  implementation(externalDependency.avroCompiler) {
    exclude group: 'org.apache.velocity', module: 'velocity'
  }
  constraints {
    implementation('commons-collections:commons-collections:3.2.2') {
      because 'Vulnerability Issue'
    }
  }

  avsc project(':metadata-events:mxe-schemas')
}

def genDir = file("src/generated/java")

task avroCodeGen(type: com.github.davidmc24.gradle.plugin.avro.GenerateAvroJavaTask, dependsOn: configurations.avsc) {
  source("$rootDir/metadata-events/mxe-schemas/src/renamed/avro")
  outputDir = genDir
  dependsOn(':metadata-events:mxe-schemas:renameNamespace')
}

compileJava.source(avroCodeGen.outputs)

idea {
  module {
    sourceDirs += genDir
    generatedSourceDirs += genDir
  }
}

project.rootProject.tasks.idea.dependsOn(avroCodeGen)

clean {
    delete 'src'
}
